plugins {
    id 'java-platform'
    id 'maven-publish'
}

repositories {
    jcenter()
}

dependencies {
    // The platform declares constraints on all components that require alignment
    constraints {
        api project(':kotysa-core')
        api project(':kotysa-android')
        api project(':kotysa-spring-jdbc')
        api project(':kotysa-spring-r2dbc')

        api "org.ufoss.kolog:kolog:$kolog_version"
    }
}

publishing {
    repositories {
        maven {
            def user = 'ufoss'
            def repo = 'ufoss'
            def name = 'kotysa'
            url = "https://api.bintray.com/maven/$user/$repo/$name/;publish=0"

            credentials {
                username = project.hasProperty('bintray_user') ? project.property('bintray_user') : System.getenv('BINTRAY_USER')
                password = project.hasProperty('bintray_api_key') ? project.property('bintray_api_key') : System.getenv('BINTRAY_API_KEY')
            }
        }
    }

    publications {
        myPlatform(MavenPublication) {
            from components.javaPlatform
        }
    }
}


