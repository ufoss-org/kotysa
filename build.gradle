plugins {
    id 'org.jetbrains.kotlin.jvm' apply false
    id 'org.jetbrains.dokka' apply false
    id 'net.researchgate.release'
}

def ossrhUsername = project.hasProperty("ossrhUsername") ? project.property("ossrhUsername") : System.getenv("OSSRH_USERNAME")
def ossrhPassword = project.hasProperty("ossrhPassword") ? project.property("ossrhPassword") : System.getenv("OSSRH_PASSWORD")

configure(subprojects.findAll { it.name != "kotysa-platform" }) {
    apply plugin: 'org.jetbrains.kotlin.jvm'
    apply plugin: 'jacoco'
    apply plugin: 'org.jetbrains.dokka'

    // Regular java modules need 'java-library' plugin for proper publication
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    apply plugin: 'signing'

    repositories {
        jcenter()
        maven { url 'https://repo.spring.io/milestone' }
        maven { url 'https://kotlin.bintray.com/kotlinx' }
    }

    dependencies {
        // import BOM
        testImplementation platform("org.junit:junit-bom:$junit_bom_version")

        testImplementation "org.assertj:assertj-core:$assertj_version"

        testRuntimeOnly "org.slf4j:slf4j-simple:$slf4j_version"
    }

    configurations {
        all*.exclude module : 'spring-boot-starter-logging'
    }

    compileJava {
        sourceCompatibility = 1.8
    }

    compileKotlin {
        kotlinOptions {
            freeCompilerArgs = ['-Xjvm-default=enable', '-Xexplicit-api=strict']
            jvmTarget = '1.8'
        }
    }

    compileTestJava {
        sourceCompatibility = 8
    }

    compileTestKotlin {
        kotlinOptions {
            freeCompilerArgs = ['-Xjvm-default=enable']
            jvmTarget = '1.8'
        }
    }

    test {
        useJUnitPlatform()
        testLogging {
            events 'passed', 'failed', 'skipped'
            showStandardStreams = true
        }
    }

    jacoco {
        toolVersion = '0.8.5'
    }

    tasks.withType(Test) {
        jacoco.includeNoLocationClasses = true
        jacoco.excludes = ['jdk.internal.*']
    }

    jacocoTestReport {
        dependsOn(test)

        reports {
            html.enabled = true
            xml.enabled = true
        }
    }

    // --------------- Source & Javadoc artefacts + publishing ---------------

    // generate xxx-sources.jar
    java {
        withSourcesJar()
    }

    tasks.dokkaHtml.configure {
        outputDirectory.set(file("$buildDir/javadoc"))

        dokkaSourceSets {
            configureEach {
                jdkVersion.set(8)
            }
        }
    }

    // generate xxx-javadoc.jar
    task javadocJar(type: Jar, dependsOn: dokkaHtml) {
        from "$buildDir/javadoc"
        archiveClassifier = 'javadoc'
    }

    publishing {
        repositories {
            maven {
                if (project.version.toString().endsWith("SNAPSHOT")) {
                    setUrl("https://s01.oss.sonatype.org/content/repositories/snapshots/")
                } else {
                    setUrl("https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/")
                }

                credentials {
                    username = ossrhUsername
                    password = ossrhPassword
                }
            }
        }

        publications {
            maven(MavenPublication) {
                artifactId = project.name
                from components.java
                artifact javadocJar
                pom {
                    name = project.name
                    description = 'Kotysa is the idiomatic way to write Kotlin type-safe SQL'
                    url = 'https://github.com/ufoss-org/kotysa'

                    licenses {
                        license {
                            name = 'The Unlicence'
                            url = 'https://unlicense.org'
                        }
                    }

                    developers {
                        developer {
                            name = 'pull-vert'
                            url = 'https://github.com/pull-vert'
                        }
                    }

                    scm {
                        connection = 'scm:git:https://github.com/ufoss-org/kotysa'
                        developerConnection = 'scm:git:git://github.com/ufoss-org/kotysa.git'
                        url = 'https://github.com/ufoss-org/kotysa.git'
                    }
                }
            }
        }
    }

    signing {
        // Require signing.keyId, signing.password and signing.secretKeyRingFile
        sign(publishing.publications)
    }
}

// Workaround for project with modules https://github.com/researchgate/gradle-release/issues/144
task releaseBuild {
    dependsOn subprojects.findResults { it.tasks.findByName('build') }
}

release {
    buildTasks = ['releaseBuild']
}

// when version changes :
// -> execute ./gradlew wrapper, then delete .gradle directory, then execute ./gradlew wrapper again
wrapper {
    gradleVersion = '6.8.3'
    distributionType = Wrapper.DistributionType.ALL
}
