plugins {
    id 'org.jetbrains.kotlin.jvm' apply false
    id 'org.jetbrains.dokka' apply false
    id 'net.researchgate.release'
}

configure(subprojects.findAll { it.name != "kotysa-platform" }) {
    apply plugin: 'org.jetbrains.kotlin.jvm'
    apply plugin: 'jacoco'
    apply plugin: 'org.jetbrains.dokka'

    // Regular java modules need 'java-library' plugin for proper publication
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'

    repositories {
        jcenter()
        maven { url 'https://repo.spring.io/milestone' }
        maven { url 'https://kotlin.bintray.com/kotlinx' }
    }

    dependencies {
        // import BOM
        testImplementation platform("org.junit:junit-bom:$junit_bom_version")

        testImplementation "org.assertj:assertj-core:$assertj_version"

        testRuntimeOnly "org.slf4j:slf4j-simple:$slf4j_version"
    }

    configurations {
        all*.exclude module : 'spring-boot-starter-logging'
    }

    compileJava {
        sourceCompatibility = 1.8
    }

    compileKotlin {
        kotlinOptions {
            freeCompilerArgs = ['-Xjvm-default=enable', '-Xexplicit-api=strict']
            jvmTarget = '1.8'
        }
    }

    compileTestJava {
        sourceCompatibility = 8
    }

    compileTestKotlin {
        kotlinOptions {
            freeCompilerArgs = ['-Xjvm-default=enable']
            jvmTarget = '1.8'
        }
    }

    test {
        useJUnitPlatform()
        testLogging {
            events 'passed', 'failed', 'skipped'
            showStandardStreams = true
        }
    }

    jacoco {
        toolVersion = '0.8.5'
    }

    tasks.withType(Test) {
        jacoco.includeNoLocationClasses = true
        jacoco.excludes = ['jdk.internal.*']
    }

    jacocoTestReport {
        dependsOn(test)

        reports {
            html.enabled = true
            xml.enabled = true
        }
    }

    // --------------- Source & Javadoc artefacts + publishing ---------------

    // generate xxx-sources.jar
    java {
        withSourcesJar()
    }

    tasks.dokkaHtml.configure {
        outputDirectory.set(file("$buildDir/javadoc"))

        dokkaSourceSets {
            configureEach {
                jdkVersion.set(8)
            }
        }
    }

    // generate xxx-javadoc.jar
    task javadocJar(type: Jar, dependsOn: dokkaHtml) {
        from "$buildDir/javadoc"
        archiveClassifier = 'javadoc'
    }

    publishing {
        repositories {
            maven {
                def user = 'ufoss'
                def repo = 'ufoss'
                def name = 'kotysa'
                url = "https://api.bintray.com/maven/$user/$repo/$name/;publish=0"

                credentials {
                    username = project.hasProperty('bintray_user') ? project.property('bintray_user') : System.getenv('BINTRAY_USER')
                    password = project.hasProperty('bintray_api_key') ? project.property('bintray_api_key') : System.getenv('BINTRAY_API_KEY')
                }
            }
        }

        publications {
            maven(MavenPublication) {
                artifactId = project.name
                from components.java
                artifact javadocJar
                pom {
                    name = project.name
                    description = 'Kotysa is the idiomatic way to write Kotlin type-safe SQL'
                    url = 'https://github.com/ufoss-org/kotysa'
                    licenses {
                        license {
                            name = 'The Unlicence'
                            url = 'https://unlicense.org'
                        }
                    }
                    scm {
                        connection = 'scm:git:git://github.com/ufoss-org/kotysa.git'
                        url = 'https://github.com/ufoss-org/kotysa.git'
                    }
                }
            }
        }
    }
}

// Workaround for project with modules https://github.com/researchgate/gradle-release/issues/144
task build {
    dependsOn subprojects.findResults { it.tasks.findByName('build') }
}

// when version changes :
// -> execute ./gradlew wrapper, then delete .gradle directory, then execute ./gradlew wrapper again
wrapper {
    gradleVersion = '6.7'
    distributionType = Wrapper.DistributionType.ALL
}
